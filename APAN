:"Pan with        arrow keys
:"Zoom with       + and -         Set axis with   '(' > X         ')' > Y         ',' > BOTH
:"Set precision   with 1-9
:"Set live        updates with   ENTER/GRAPH
:"Quit with       CLEAR
:
:
:
:
:
:"ʟAPANS(1) subprogram
:"ʟAPAN1 backup original vars
:"ʟAPAN2 backup APAN vars
:"P precision
:"L live
:"S scale axis
:"D need redraw
:"K key
:"W width
:"H height
:"U center X
:"V center Y
:
:SetUpEditor APANS
:If dim(ʟAPANS)=0:Then:{0}→ʟAPANS:End
:If ʟAPANS(1)=11:Then
:{P,L,S,D,K,W,H,U,V}→ʟAPAN1
:Goto Q:End
:If ʟAPANS(1)=12:Then
:{P,L,S,D,K,W,H,U,V}→ʟAPAN2
:Goto Q:End
:If ʟAPANS(1)=21:Then
:ʟAPAN1(1)→P
:ʟAPAN1(2)→L
:ʟAPAN1(3)→S
:ʟAPAN1(4)→D
:ʟAPAN1(5)→K
:ʟAPAN1(6)→W
:ʟAPAN1(7)→H
:ʟAPAN1(8)→U
:ʟAPAN1(9)→V
:Goto Q:End
:If ʟAPANS(1)=22:Then
:ʟAPAN2(1)→P
:ʟAPAN2(2)→L
:ʟAPAN2(3)→S
:ʟAPAN2(4)→D
:ʟAPAN2(5)→K
:ʟAPAN2(6)→W
:ʟAPAN2(7)→H
:ʟAPAN2(8)→U
:ʟAPAN2(9)→V
:Goto Q:End
:
:11→ʟAPANS(1):prgmAPAN
:2→P
:1→L
:0→S
:1→D
:While 1
:getKey→K
:Xmax-Xmin→W
:Ymax-Ymin→H
:(Xmin+Xmax)/2→U
:(Ymin+Ymax)/2→V
:If K=45:Then
::Goto C
:End
:If L and D and K=0:Then
::0→D
::12→ʟAPANS(1):prgmAPAN
::21→ʟAPANS(1):prgmAPAN
::DispGraph
::22→ʟAPANS(1):prgmAPAN
:End
:If K=24:Then
::Xmin-W/P→Xmin
::Xmax-W/P→Xmax
::1→D
:End
:If K=26:Then
::Xmin+W/P→Xmin
::Xmax+W/P→Xmax
::1→D
:End
:If K=25:Then
::Ymin+H/P→Ymin
::Ymax+H/P→Ymax
::1→D
:End
:If K=34:Then
::Ymin-H/P→Ymin
::Ymax-H/P→Ymax
::1→D
:End
:If K=85:Then
::If S≠2:Then
:::U-W/2-(W/P)→Xmin
:::U+W/2+(W/P)→Xmax
::End
::If S≠1:Then
:::V-H/2-(H/P)→Ymin
:::V+H/2+(H/P)→Ymax
::End
::1→D
:End
:If K=95:Then
::If S≠2:Then
:::U-W/2+(W/(P+2))→Xmin
:::U+W/2-(W/(P+2))→Xmax
::End
::If S≠1:Then
:::V-H/2+(H/(P+2))→Ymin
:::V+H/2-(H/(P+2))→Ymax
::End
::1→D
:End
:If 62≤K and K≤64:Then:K-62+0→S:End
:If 72≤K and K≤74:Then:K-72+7→P:End
:If 82≤K and K≤84:Then:K-82+4→P:End
:If 92≤K and K≤94:Then:K-92+1→P:End
:If K=105:Then:0→L:End
:If K=15:Then:1→L:End
:End
:
:Lbl C
:21→ʟAPANS(1):prgmAPAN
:DelVar ʟAPANS
:DelVar ʟAPAN1
:DelVar ʟAPAN2
:SetUpEditor 
:
:Lbl Q
